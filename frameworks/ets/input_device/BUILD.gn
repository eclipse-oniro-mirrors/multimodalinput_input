# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
# http://www.apache.org/licenses/LICENSE-2.0
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/multimodalinput/input/multimodalinput_mini.gni")
copy_taihe_idl("input_device_taihe") {
  sources = [ "idl/ohos.multimodalInput.inputDevice.taihe" ]
  deps = [ "${mmi_path}/frameworks/ets/key_code:key_code_taihe" ]
}
config("inputDevice_config") {
  visibility = [ ":*" ]

  include_dirs = [
    "./include",
    "${mmi_path}/util/common/include",
    "${mmi_path}/util/network/include",
    "${mmi_path}/frameworks/ets/common/include",
    "${mmi_path}/frameworks/ets/key_code/include"
  ]
}
subsystem_name = "multimodalinput"
part_name = "input"
taihe_generated_file_path_inputDevice =
    "$taihe_file_path/out/$subsystem_name/$part_name/inputDevice"
ohos_taihe("run_taihe") {
  taihe_generated_file_path = "${taihe_generated_file_path_inputDevice}"
  deps = [ ":input_device_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.multimodalInput.inputDevice.ani.cpp",
    "$taihe_generated_file_path/src/ohos.multimodalInput.inputDevice.abi.c",
  ]
}
taihe_shared_library("InputDevice") {
  taihe_generated_file_path = "${taihe_generated_file_path_inputDevice}"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  configs = [
    "${mmi_path}:coverage_flags",
    ":inputDevice_config",
  ]
  sources += [
    "${mmi_path}/frameworks/ets/key_code/src/ohos.multimodalInput.keyCode.impl.cpp",
    "${mmi_path}/frameworks/ets/common/src/ani_common.cpp",
    "src/ani_constructor.cpp",
    "src/ohos.multimodalInput.inputDevice.impl.cpp",
    "src/taihe_event.cpp",
    "src/taihe_input_device_utils.cpp",
  ]
  deps = [
    ":run_taihe",
    "${mmi_path}/frameworks/proxy:libmmi-client",
    "${mmi_path}/util:libmmi-util",
  ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
  ]
  branch_protector_ret = "pac_ret"
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
}
generate_static_abc("input_device") {
  base_url = "${taihe_generated_file_path_inputDevice}"
  files = [ "${taihe_generated_file_path_inputDevice}/@ohos.multimodalInput.inputDevice.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/input_device.abc"
  dependencies = [ ":run_taihe" ]
}
ohos_prebuilt_etc("input_device_etc") {
  source = "$target_out_dir/input_device.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [ ":input_device" ]
}

generate_static_abc("input_device_taihe_test") {
  base_url = "${mmi_path}/frameworks/ets/input_device"
  files = [ "$base_url/test/test_main.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/input_device_test.abc"
}


group("ets_inputDevice_package") {
  deps = [
    ":InputDevice",
    ":input_device_etc",
  ]
}
